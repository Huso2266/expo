import groovy.json.JsonSlurper
import java.util.regex.Matcher
import java.util.regex.Pattern

def publishGroup = 'host.exp.exponent'
version = '0.9.0'

// reanimated v2
def reactNativeFilePath = ["node", "--print", "require.resolve('react-native/package.json')"].execute(null, rootDir).text.trim()
def inputFile = new File(reactNativeFilePath)
def json = new JsonSlurper().parseText(inputFile.text)
def reactNativeVersion = json.version as String
def (major, minor, patch) = reactNativeVersion.tokenize('.')

def engine = "jsc"

// If the user has the reanimated 2.2.1 or above installed, we don't have to swap JNI implementation.
def shouldSwapJNI() {
  try {
    def reanimatedVersion = ["node", "--print", "require('react-native-reanimated/package.json').version"].execute(null, rootDir).text.trim()
    if (reanimatedVersion == "undefined") {
      return true
    }
    def (int major, int minor, int patch) = reanimatedVersion.tokenize('.').collect { it.toInteger() }

    if (major < 2 || (major == 2 && minor <= 1) || (major == 2 && minor == 2 && patch == 0)) {
      return true
    }

    return !rootProject.findProject(":react-native-reanimated")
  } catch (ignored) {
    return true
  }
}

abstract class replaceSoTask extends DefaultTask {
  public static String appName = ":app"
  public static String buildDir = "../../../android/app/build"

  @TaskAction
  def run() {
    for (def abiVersion in ["x86", "x86_64", "armeabi-v7a", "arm64-v8a"]) {
      ant.sequential {
        copy(
            tofile: "${buildDir}/intermediates/merged_native_libs/debug/out/lib/${abiVersion}/libfbjni.so",
            file: "../vendored/react-native-reanimated/libSo/fbjni/jni/${abiVersion}/libfbjni.so",
            overwrite: true
        )
      }
    }
  }
}

def getCurrentFlavor() {
  Gradle gradle = getGradle()
  String taskRequestName = gradle.getStartParameter().getTaskRequests().toString()
  Pattern pattern

  if (taskRequestName.contains("assemble")) {
    pattern = Pattern.compile("assemble(\\w+)(Release|Debug)")
  } else {
    pattern = Pattern.compile("generate(\\w+)(Release|Debug)")
  }
  Matcher matcher = pattern.matcher(taskRequestName)

  if (matcher.find()) {
    return matcher.group(1).toLowerCase()
  }
  return ""
}

def replaceSoTaskDebug
def replaceSoTaskRelease

if (Integer.parseInt(minor) < 65 && shouldSwapJNI()) {
  println("DevMenu will replace JNI library.")

  tasks.register("replaceSoTaskDebug", replaceSoTask)
  tasks.register("replaceSoTaskRelease", replaceSoTask)
  replaceSoTaskDebug = project.getTasks().getByPath(":expo-dev-menu:replaceSoTaskDebug")
  replaceSoTaskRelease = project.getTasks().getByPath(":expo-dev-menu:replaceSoTaskRelease")
}

rootProject.getSubprojects().forEach({ project ->
  if (project.plugins.hasPlugin("com.android.application")) {
    if (project.ext.has("react") && project.ext.react.enableHermes) {
      engine = "hermes"
    }

    if (project.getProperties().get("android") && Integer.parseInt(minor) < 65 && shouldSwapJNI()) {
      def projectProperties = project.getProperties()
      if (!projectProperties.get("reanimated")
          || (projectProperties.get("reanimated") && projectProperties.get("reanimated").get("enablePackagingOptions"))
      ) {
        def flavorString = getCurrentFlavor()
        replaceSoTask.appName = project.getProperties().path
        replaceSoTask.buildDir = project.getProperties().buildDir
        def appName = project.getProperties().path

        replaceSoTaskDebug.dependsOn(
            project.getTasks().getByPath("${appName}:merge${flavorString}DebugNativeLibs"),
            project.getTasks().getByPath("${appName}:strip${flavorString}DebugDebugSymbols")
        )
        project.getTasks().getByPath("${appName}:package${flavorString}Debug").dependsOn(replaceSoTaskDebug)

        replaceSoTaskRelease.dependsOn(
            project.getTasks().getByPath("${appName}:merge${flavorString}ReleaseNativeLibs"),
            project.getTasks().getByPath("${appName}:strip${flavorString}ReleaseDebugSymbols")
        )
        project.getTasks().getByPath("${appName}:package${flavorString}Release").dependsOn(replaceSoTaskRelease)
      }
    }
  }
})

def reanimatedPath = "../vendored/react-native-reanimated/android"
def minorCopy = Integer.parseInt(minor)
def reanimatedFolder = file("${reanimatedPath}/${minorCopy}-${engine}")
while (!reanimatedFolder.exists()) {
  minorCopy -= 1
  reanimatedFolder = file("${reanimatedPath}/${minorCopy}-${engine}")
  if (minorCopy < 62) {
    throw new GradleException('No matching aar for react-native-reanimated found. Upgrade react-native to 0.63.x or later in order to use the expo-dev-menu package.')
  }
}
def reanimatedVersion = minorCopy

// end of reanimated v2

if (System.env.MAVEN_EAS) {
  def url = new URL(System.env.MAVEN_EAS)
  def foundAarFile = false
  if (url.getProtocol() == 'file') {
    def aarFile = fileTree(dir: url.getFile(), include: "**/${name}/${version}/*.aar").getFiles()[0]
    foundAarFile = !!aarFile
  } else {
    url = new URL("${url.toString()}/${publishGroup.replace('.', '/')}/${name}/${version}/${name}-${version}.pom")
    def conn = url.openConnection()
    conn.setRequestMethod('HEAD')
    conn.connect()
    def status = conn.getResponseCode()
    foundAarFile = status >= 200 && status < 300
  }
  if (foundAarFile) {
    ext.isAarProject = true
    configurations.maybeCreate('default')
    dependencies.add('default', "${publishGroup}:${name}:${version}")
    return
  }
}

group = publishGroup

apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'maven'

buildscript {
  // Simple helper that allows the root project to override versions declared by this library.
  ext.safeExtGet = { prop, fallback ->
    rootProject.ext.has(prop) ? rootProject.ext.get(prop) : fallback
  }

  repositories {
    mavenCentral()
  }

  dependencies {
    classpath("org.jetbrains.kotlin:kotlin-gradle-plugin:${safeExtGet('kotlinVersion', '1.4.21')}")
  }
}

// Upload android library to maven with javadoc and android sources
configurations {
  deployerJars
}

// Creating sources with comments
task androidSourcesJar(type: Jar) {
  classifier = 'sources'
  from android.sourceSets.main.java.srcDirs
}

// Put the androidSources and javadoc to the artifacts
artifacts {
  archives androidSourcesJar
}

uploadArchives {
  repositories {
    mavenDeployer {
      configuration = configurations.deployerJars
      if (System.env.MAVEN_PUBLISH_EAS) {
        repository(url: System.env.MAVEN_PUBLISH_EAS) {
          if (System.env.MAVEN_PUBLISH_EAS_USERNAME && System.env.MAVEN_PUBLISH_EAS_PASSWORD) {
            authentication(userName: System.env.MAVEN_PUBLISH_EAS_USERNAME, password: System.env.MAVEN_PUBLISH_EAS_PASSWORD)
          }
        }
      } else {
        repository(url: mavenLocal().url)
      }
    }
  }
}

android {
  compileSdkVersion safeExtGet("compileSdkVersion", 30)

  compileOptions {
    sourceCompatibility JavaVersion.VERSION_1_8
    targetCompatibility JavaVersion.VERSION_1_8
  }

  defaultConfig {
    minSdkVersion safeExtGet("minSdkVersion", 21)
    targetSdkVersion safeExtGet("targetSdkVersion", 30)
    versionCode 10
    versionName '0.9.0'
  }
  lintOptions {
    abortOnError false
  }

  buildTypes {
    releaseWithDevMenu {
      initWith release
      matchingFallbacks = ['release', 'debug']
    }
  }

  sourceSets {
    main {
      jniLibs.srcDir "$reanimatedPath/$reanimatedVersion-$engine/jni"
      java {
        srcDirs += ['../vendored/react-native-gesture-handler/android/']
        srcDirs += ['../vendored/react-native-safe-area-context/android/']
      }
    }

    releaseWithDevMenu {
      setRoot 'src/debug'
    }
  }
}

task copyAssets(type: Copy) {
  from('../assets') {
    exclude "*.ios.*"
  }
  into 'src/main/assets'
}

project.task uploadExpoArchives {
  dependsOn(uploadArchives)
}
